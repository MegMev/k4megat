file(GLOB sources src/*.cc)
file(GLOB headers RELATIVE ${CMAKE_CURRENT_LIST_DIR} include/Analysis/*.h)
list(FILTER headers EXCLUDE REGEX "LinkDef.h")

add_library(MegatUtils SHARED  ${sources} )
add_library(Megat::MegatUtils ALIAS MegatUtils)

target_include_directories(MegatUtils  PUBLIC
                           $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
                           $<INSTALL_INTERFACE:include>
                           )


target_link_libraries(MegatUtils PUBLIC
                      ROOT::Physics
                      ROOT::MathCore
                      ROOT::ROOTVecOps
                      ROOT::ROOTDataFrame
                      EDM4HEP::edm4hep
                      EDM4HEP::edm4hepDict
                      podio::podio
                      DD4hep::DDCore
                      )

set_target_properties(MegatUtils PROPERTIES
  PUBLIC_HEADER "${headers}")

ROOT_GENERATE_DICTIONARY(G__MegatUtils
                         ${headers}
                         MODULE MegatUtils
                         LINKDEF include/Analysis/LinkDef.h
                         )

install(TARGETS MegatUtils
    EXPORT MegatTargets
    ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
    LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
    PUBLIC_HEADER DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/Utility"
    )

install(FILES
  "${PROJECT_BINARY_DIR}/utility/libMegatUtils.rootmap"
    DESTINATION "${CMAKE_INSTALL_LIBDIR}")

if (${ROOT_VERSION} GREATER 6)
    install(FILES
          "${PROJECT_BINARY_DIR}/utility/libMegatUtils_rdict.pcm"
                DESTINATION "${CMAKE_INSTALL_LIBDIR}")
    endif()
